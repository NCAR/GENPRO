      SUBROUTINE RDDATA (INUNIT,T1,T2,NRECRDS,NPARMS,INDEX,ARRAY,NHDR,
     1 IFLAG,M,N,NST)
C
CCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCC
C                                                                      C
C        THIS SUBROUTINE IS TO BE USED IN CONJUNCTION WITH SUBROUTINES C
C        RDHDR, AND RDREC0.  THIS ROUTINE ACTS AS A DRIVER FOR THE     C
C        OTHERS, ACCEPTING A START AND FINISH TIME AND A MAXIMUM NUMBERC
C        OF RECORDS TO BE RETURNED.  THESE RECORDS CONSIST ONLY OF     C
C        SELECTED PARAMETERS, GIVEN BY THE VALUES OF THE ARRAY INDEX.  C
C                                                                      C
C        ENTRY RDDAT2 ALLOWS MORE RECORDS TO BE READ FROM THE FILE     C
C        WITHOUT TRYING TO READ A HEADER RECORD.                       C
C                                                                      C
C                                                                      C
C        THE CALL IS                                                   C
C             CALL RDDATA (INUNIT,T1,T2,NRECRDS,NPARMS,INDEX,ARRAY,    C
C                          NHDR,IFLAG)                                 C
C        INUNIT    -  LOGICAL UNIT TAPE IS MOUNTED ON                  C
C        T1        -  START TIME FOR SAMPLING.                         C
C        T2        -  END TIME OF SAMPLING PERIOD.                     C
C        NRECRDS   -  MAXIMUM NUMBER OF RECORDS TO BE RETURNED.  IF YOUC
C                     WANT ALL THE RECORDS BETWEEN T1 AND T2, BE SURE  C
C                     NRECRDS >= (T2-T1)/CYCTIME +1.                   C
C                     ON RETURN, NRECRDS HOLDS THE NUMBER OF RECORDS   C
C                     ACTUALLY RETURNED.                               C
C        NPARMS    -  NUMBER OF PARAMETERS TO BE RETURNED FOR EACH     C
C                     RECORD.                                          C
C        INDEX     -  ARRAY CONTAINING THE LOCATIONS OF THE PARAMETERS C
C                     WITHIN A RECORD TO BE RETURNED.  THESE NEED BE   C
C                     IN NO SPECIAL ORDER.                             C
C        ARRAY     -  THE ARRAY TO CONTAIN THE RETURNED VALUES.  IT IS C
C                     A TWO-DIMENSIONAL ARRAY. WITH THE FIRST INDEX    C
C                     REFERRING TO RECORD NUMBER, AND THE SECOND TO    C
C                     PARAMETER NUMBER (NOT LOCATION).                 C
C        NHDR      -  GIVES THE NUMBER OF PHYSICAL RECORDS IN THE HEADER
C        IFLAG     -  AS INPUT, CONTROLS THE PRINTING OF THE HEADER    C
C                     INFORMATION.  IF IFLAG = 1, THE PRINTING WILL BE C
C                     SUPPRESSED.                                      C
C                     AS OUTPUT, IT INDICATES THE COMPLETION CODE.     C
C                     IFLAG=0, NORMAL RETURN, WITH T2 LIMITING SEARCH. C
C                     IFLAG=1, NORMAL RETURN, NRECRDS LIMITING SEARCH  C
C                     IFLAG=2, T1 AND T2 HAVE INVALID TIMES.           C
C                     IFLAG=3, NRECRDS OR NPARMS IS < ZERO.            C
C                     IFLAG=4, EOF WAS REACHED ON A HEADER READ.       C
C                     IFLAG=5, UNRECOVERABLE PARITY ERROR OCCURRED.    C
C                     IFLAG=6, EOF WAS REACHED DURING DATA READ.       C
C                     IFLAG=7, CATASTROPHIC ERRORS HAVE OCCURRED IN    C
C                              EXECUTION.                              C
C        M         -  FIRST DIMENSION OF INPUT ARRAY "ARRAY"           C
C        N         -  SECOND DIMENSION OF INPUT ARRAY "ARRAY"          C
C        NST       -  RECORDS WILL BE STORED IN "ARRAY" BEGINNING AT   C
C                     ARRAY(NST,1).                                    C
C                                                                      C
C        WRITTEN BY GEORGE LEHMANN, JR.                                C
C                   OCTOBER,1976                                       C
C                                                                      C
C        MODIFIED TO RUN ON THE CRAY-1 BY MIKE DANIELS                 C
C                   JANUARY, 1983                                      C
C                                                                      C
C                                                                      C
CCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCC
C
      DIMENSION INDEX(NPARMS),ARRAY(M,N),IRATE(100),
     C  SCALE(100),BIAS(100),INARRAY(1500),DATARAY(1500)
      DATA  ISWCH /0/
      IF(T2.LE.T1) GO TO 900
      IF (NRECRDS.LE.0.OR.NPARMS.LE.0) GO TO 910
      IF (ISWCH.EQ.1) GO TO 100
      ISWCH = 1
C
C        PROCESS HEADER INFORMATION
C
      CALL RDHDR (INUNIT,IRATE,SCALE,BIAS,NP,NPER,CTIME,NR,NW,INARRAY,
     C NHDR,IFLAG)
      IFLAG=IFLAG+1
      GO TO (100,920,930,90),IFLAG
   90 WRITE (6,91) INUNIT
C
C        THIS ENTRY POINT ALLOWS READING DATA WITHOUT FIRST READING
C        THE HEADER
C
      ENTRY RDDAT2
  100 N=NST-1
      T0=T1-FLOAT(NR)*CTIME
C
C        SEARCH FOR THE RIGHT RECORDS
C
  110 IFLAG=1
  120 CALL RDREC (INUNIT,IRATE,SCALE,BIAS,NP,NPER,NR,DATARAY,INARRAY,
     C IFLAG)
      IF (IFLAG.EQ.0) GO TO 200
      IFLAG=IFLAG-89
      GO TO (940,950,950,930,190), IFLAG
  190 WRITE (6,91) INUNIT
  200 IF (DATARAY(1).LT.T0) GO TO 110
      IF (DATARAY(1).LT.T1) GO TO 120
      IF (DATARAY(1).GT.T2) GO TO 800
C
C        MOVE PARAMETERS INTO NEW ARRAY ELEMENT
C
      N=N+1
      DO 300 J=1,NPARMS
      K=INDEX(J)
  300 ARRAY(N,J)=DATARAY(K)
      IF (N.GE.NRECRDS) GO TO 810
      GO TO 120
C
C        FINISH UP AND RETURN
C
  800 NRECRDS=N
      IFLAG=0
      RETURN
  810 NRECRDS=N
      IFLAG=1
      RETURN
C
  900 IFLAG=2
      WRITE (6,905)
      RETURN
C
  910 IFLAG=3
      WRITE (6,915)
      RETURN
C
  920 IFLAG=4
      ISWCH = 0
      NRECRDS=N
      WRITE (6,925)
      RETURN
C
  930  IFLAG=5
      WRITE (6,935)
      RETURN
C
  940 IFLAG=6
      ISWCH = 0
      NRECRDS=N
      WRITE (6,945)
      RETURN
C
  950 IFLAG=7
      WRITE (6,955)
      RETURN
C
   91 FORMAT ("0RECOVERABLE PARITY ERROR OCCURRED ON UNIT ",I2)
  905 FORMAT ("-  >>>ERROR<<<  INVALID VALUES FOR TIMES T1 AND T2")
  915 FORMAT ("-  >>>ERROR<<<  NUMBER OF RECORDS OR NUMBER OF PARAMETERS
     C REQUESTED IS NON-POSITIVE NUMBER")
  925 FORMAT ("-  END OF VOLUME")
  935 FORMAT ("-  >>>ERROR<<<  PARITY ERROR OCCURRED DURING TAPE READ")
  945 FORMAT ("-  END OF FILE")
  955 FORMAT ("-  >>>>>ERROR<<<<< CATASTROPHIC ERROR HAS OCCURRED")
      END
C
      SUBROUTINE RDHDR   (INUNIT,IRATE,SCALE,BIAS,NPARMS,NPERCYC,
     C  CYCTIME,NREC,NWORDS,INARRAY,NHDR,IFLAG)
CCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCC
C                                                                      C
C                                                                      C
C        THIS SUBROUTINE READS A HEADER BLOCK, PRINTS THE INFORMATION  C
C        CONTAINED IN THAT BLOCK, DECODES AND RETURNS CERTAIN PARAMETERS
C        TO THE CALLING PROGRAM.                                       C
C                                                                      C
C        THE CALL IS                                                   C
C            CALL RDHDR   (INUNIT,IRATE,SCALE,BIAS,NPARMS,NPERCYC,     C
C                           CYCTIME,NREC,NWORDS,INARRAY,IFLAG)         C
C        INUNIT    -  LOGICAL UNIT NUMBER OF THE TAPE USED             C
C        IRATE     -  ARRAY TO CONTAIN RETURNED VALUES OF SAMPLING RATEC
C        SCALE     -  ARRAY TO CONTAIN RETURNED VALUES FOR RESCALING   C
C                     THE DATA.                                        C
C        BIAS      -  ARRAY TO CONTAIN THE RETURNED VALUES FOR         C
C                     CORRECTING THE DATA OFFSET.                      C
C        NPARMS    -  RETURNS THE NUMBER OF PARAMETERS PER LOGICAL REC C
C        NPERCYC   -  RETURNS THE NUMBER OF PARAMETERS TAKEN PER CYCLE C
C        CYCTIME   -  RETURNS THE CYCLE TIME USED                      C
C        NREC      -  RETURNS THE NUMBER OF LOGICAL RECORDS PER PHYSICAL
C                     RECORD                                           C
C        NWORDS    -  RETURNS THE NUMBER OF WORDS OF MEMORY OCCUPIED   C
C                     BY A PHYSICAL RECORD                             C
C        INARRAY   -  WORKING ARRAY FOR THE SUBROUTINE, MUST BE AT     C
C                     LEAST 1500 WORDS.                                C
C        NHDR      -  GIVES NUMBER OF PHYSICAL RECORDS IN THE HEADER   C
C        IFLAG     -  AS INPUT, SUPPRESSES PRINT IF IFLAG=1            C
C                  -  AS OUTPUT, RETURNS COMPLETION CODE AS FOLLOWS    C
C                     IFLAG=0 INDICATES NORMAL RETURN                  C
C                     IFLAG=1 INDICATES EOF SENSED DURING OPERATION    C
C                     IFLAG=2 INDICATES UNRECOVERABLE PARITY ERROR     C
C                             OCCURED DURING OPERATION.                C
C                     IFLAG=3 INDICATES RECOVERABLE PARITY ERROR WAS   C
C                             DETECTED.                                C
C                                                                      C
C        WRITTEN BY GEORGE LEHMANN, JR.                                C
C                   OCTOBER,1976                                       C
C                                                                      C
CCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCC
C
      DIMENSION IRATE(1),SCALE(1),BIAS(1),INARRAY(1),LABEL(7)
      INTEGER ASCII(64),WKARRAY(16000),FLG
      COMMON /TPCORR/ ICORR1
      COMMON /LOCATN/ LOCAT(500)
      LOGICAL ICORR1
      DATA ASCII/72B,101B,102B,103B,104B,105B,106B,107B,110B,111B,112B,
     +113B,114B,115B,116B,117B,120B,121B,122B,123B,124B,125B,126B,127B,
     +130B,131B,132B,60B,61B,62B,63B,64B,65B,66B,67B,70B,71B,53B,55B,
     +52B,57B,50B,51B,44B,75B,40B,54B,56B,43B,133B,135B,45B,42B,137B,
     +41B,46B,47B,77B,74B,76B,100B,134B,136B,73B/
      NPARITY=0
      IF (IFLAG.EQ.0) WRITE(6,11)
C
C        FETCH THE HEADER RECORDS FROM TAPE
C
      IST = 1
      ICORR1 = NHDR.EQ.3
      DO 100 I=1,NHDR
    7 BUFFER IN (INUNIT,1)(INARRAY(IST),INARRAY(1500))
   10 IF (UNIT(INUNIT)) 20,991,15
   15 IF(NPARITY.NE.0) GO TO 995
      WRITE (6,996)
      NPARITY=1
      BACKSPACE INUNIT
      GO TO 7
   20 ILEN=LENGTH(INUNIT)
  100 IST=IST+ILEN
      ITER=INT((IST-1)*64/6)+1
      CALL GBYTES(INARRAY,WKARRAY,0,6,0,ITER)
      DO 102 J1=1,ITER
      IF (WKARRAY(J1).GT.63) THEN
         WKARRAY(J1)=ASCII(45)
      ELSE
         WKARRAY(J1)=ASCII(WKARRAY(J1)+1)
      ENDIF
102   CONTINUE
      CALL SBYTES(INARRAY,WKARRAY,0,8,0,ITER)
      IF(IFLAG.EQ.0) WRITE (6,150) (INARRAY(K),K=1,138)
C
C        READ RECORD DESCRIPTOR INFORMATION
C
C      DECODE (121,170,INARRAY(23)) NPARMS,NPERCYC,CYCTIME
C  170 FORMAT (I3,67X,I4,40X,F5.3)
      read(INARRAY(23), '(I3,67X,I4,40X,F5.3)') NPARMS,NPERCYC,CYCTIME
      DECODE (107,180,INARRAY(39)) NREC,NTOTAL,NWORDS
C
C        READ AND PRINT THE REMAINDER OF THE HEADER
C
      IF (IFLAG.EQ.0) WRITE (6,195)
      FLG=0
      ID=138
      LOC=1
      DO 200 I=1,NPARMS
      IF (FLG.EQ.0) THEN
         DECODE (100,210,INARRAY(ID)) J,IRATE(I),LABEL,SCALE(I),BIAS(I)
      ELSE
         DECODE (96,215,INARRAY(ID+1)) J,IRATE(I),LABEL,SCALE(I),BIAS(I)
      ENDIF
      IF (I.EQ.1) GO TO 190
      LOC=LOC+IRATE(I-1)
  190 IF(IFLAG.EQ.0) WRITE (6,220) J,LOC,IRATE(I),LABEL,SCALE(I),BIAS(I)
      LOCAT(I)=LOC
      IF (FLG.EQ.0) THEN
         ID=ID+12
         FLG=1
      ELSE
         ID=ID+13
         FLG=0
      ENDIF
  200 CONTINUE
  202 CALL RDREC0 (INUNIT,IRATE,SCALE,BIAS,NPARMS,NPERCYC,NREC,INARRAY,
     C INARRAY,IFLAG)
      IFLAG=0
      IF(NPARITY.NE.0) IFLAG=3
      RETURN
C
C        ERROR MESSAGES
C
  991 WRITE (6,992)
      IFLAG=1
      RETURN
C
  995 WRITE (6,996)
      IFLAG=2
      RETURN
C
   11 FORMAT ("1")
  150 FORMAT (10(1X,14A8,/))
  180 FORMAT (1X,I2,26X,I4,70X,I3)
  195 FORMAT (/," ITEM   LOCATION   RATE",17X,"DESCRIPTION",20X,"NAME",
     C4X,"UNITS",18X,"SCALE",12X,"BIAS",/)
  210 FORMAT (4X,I3,1X,I4,5X,7A8,10X,F7.1,3X,F7.1)
  215 FORMAT (I3,1X,I4,5X,7A8,10X,F7.1,3X,F7.1)
  220 FORMAT ((1X,I3,*)*,I8,I9,5X,7A8,17X,F9.2,5X,F9.2)
  992 FORMAT ("-  >>>ERROR<<<  EOF SENSED ON HEADER READ")
  996 FORMAT ("-  >>>ERROR<<<  PARITY ERROR SENSED ON HEADER READ")
      END
C
      SUBROUTINE RDREC0  (INUNIT,IRATE,SCALE,BIAS,NPARMS,NPERCYC,NREC,
     C  ARRAY,INARRAY,IFLAG)
C
CCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCC
C                                                                      C
C        THIS SUBROUTINE IS USED TO FETCH LOGICAL RECORDS FROM TAPE.   C
C        IT HAS TWO MODES OF OPERATION, GOVERNED BY PARAMETER IFLAG.   C
C        IN SCROLL MODE, THE NEXT SEQUENTIAL LOGICAL RECORD IS         C
C        RETURNED.  IN SELECT MODE, LOGICAL RECORD N IS RETURNED.      C
C        IF THE LOGICAL RECORD REQUESTED COMES BEFORE THE CURRENT      C
C        RECORD POINTER, THE NEXT PHYSICAL RECORD IS READ TO OBTAIN    C
C        THE CORRECT LOGICAL RECORD.                                   C
C                                                                      C
C        A CALL TO RDREC0 INITIALIZES THE POINTER AND ENABLES THE READ C
C        OPERATION.  THIS CALL IS NORMALLY MADE BY SUBROUTINE RDHDR.   C
C        ENTRY POINT RDREC IS THE CALL USED TO READ A LOGICAL RECORD.  C
C                                                                      C
C        THE CALL IS                                                   C
C          CALL RDREC (INUNIT,IRATE,SCALE,BIAS,NPARMS,NPERCYC,NREC,    C
C                      ARRAY,INARRAY,IFLAG                             C
C                                                                      C
C        INUNIT    -  THE NUMBER OF THE LOGICAL UNIT THE TAPE IS MOUNTED
C                     ON                                               C
C        IRATE     -  THE ARRAY CONTAINING THE SAMPLE RATES FOR THE    C
C                     PARAMETERS (RETURNED FROM RDHDR)                 C
C        SCALE     -  THE ARRAY CONTAINING THE SCALING FACTORS FOR THE C
C                     PARAMETERS (RETURNED FROM RDHDR)                 C
C        BIAS      -  THE ARRAY CONTAINING THE OFFSET VALUES FOR THE   C
C                     PARAMETERS (RETURNED FROM RDHDR)                 C
C        NPARMS    -  NUMBER OF PARAMETERS TO BE FOUND IN A LOGICAL    C
C                     RECORD                                           C
C        NPERCYC   -  TOTAL NUMBER OF DATA ITEMS PER LOGICAL RECORD    C
C        NREC      -  NUMBER OF LOGICAL RECORDS PER PHYSICAL RECORD    C
C        ARRAY     -  REAL ARRAY IN WHICH THE PROPERLY BIASED AND      C
C                     SCALED DATA ITEMS WILL BE RETURNED               C
C        INARRAY   -  WORKING ARRAY FOR PHYSICAL RECORD STORAGE.  MUST C
C                     BE AT LEAST 1500 WORDS LONG.                     C
C        IFLAG     -  AS INPUT, CONTROLS THE MODE OF OPERATION AND THE C
C                     LOGICAL RECORD TO BE RETURNED                    C
C                     IFLAG=0 SELECTS SCROLL MODE                      C
C                     IFLAG=1,2,...,NPARMS SELECTS SELECT MODE, WITH   C
C                     THE VALUE OF IFLAG CHOOSING THE LOGICAL RECORD   C
C                     TO BE RETURNED.  IFLAG<0 OR IFLAG>NPARMS WILL    C
C                     PRODUCE AN ERROR MESSAGE.                        C
C                     AS OUTPUT, IFLAG INDICATE THE COMPLETION CODE    C
C                     IFLAG=0  INDICATES NORMAL COMPLETION             C
C                     IFLAG=90 INDICATES EOF WAS ENCOUNTERED           C
C                     IFLAG=91 INDICATES AN INVALID VALUE FOR IFLAG    C
C                     IFLAG=92 INDICATES THE SUBROUTINE WAS DISABLED   C
C                     IFLAG=93 INDICATES AN UNRECOVERABLE PARITY ERROR C
C                              OCCURRED.                               C
C                     IFLAG=94 INDICATES A RECOVERABLE PARITY ERROR    C
C                              OCCURRED.                               C
C                                                                      C
C        WRITTEN BY GEORGE LEHMANN, JR.                                C
C                   OCTOBER,1976                                       C
C                                                                      C
CCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCC
C
      DIMENSION IRATE(1),SCALE(1),BIAS(1),ARRAY(1),INARRAY(1)
      COMMON /TPCORR/ RHOTDC
      LOGICAL RHOTDC
      DATA RHOTDC /.TRUE./
      INTEGER ENABLE
      DATA ENABLE /1/
      LOGREC=NREC+1
      ENABLE=0
      ITEMS=NPERCYC*NREC
      RETURN
C
C        BEGIN NORMAL READ SEQUENCE
C
      ENTRY RDREC(INUNIT,IRATE,SCALE,BIAS,NPARMS,NPERCYC,NREC,ARRAY,
     +            INARRAY,IFLAG)
C
      NPARITY=0
      IF (IFLAG.GT.NREC) GO TO 970
      IF (IFLAG.LT.0) GO TO 960
      IF (ENABLE.NE.0) GO TO 980

      IF (LOGREC.LE.NREC.AND.IFLAG.EQ.0.OR.IFLAG.GT.LOGREC) GO TO 100
C
C        READ IN NEW PHYSICAL RECORD
C
    5 BUFFER IN (INUNIT,1)(INARRAY(1001),INARRAY(1500))
   10 IF (UNIT(INUNIT)) 20,950,990
   20 LOGREC=1
      NLENG=LENGTH(INUNIT)
      IF (IFLAG.NE.0) LOGREC=IFLAG
      CALL GBYTES (INARRAY(1001),INARRAY(1),0,20,0,ITEMS)
  100 ISTART=(LOGREC-1)*NPERCYC
      ISET=1
      DO 200 J=1,NPARMS
      J2=IRATE(J)
      DO 200 K=1,J2
      ARRAY(ISET)=FLOAT(INARRAY(ISTART+ISET))/SCALE(J)-BIAS(J)
  200 ISET=ISET+1
      IF (RHOTDC) ARRAY(7)=ARRAY(7)*(ARRAY(6)+273.16)/(ARRAY(4)+273.16)
      LOGREC=LOGREC+1
      IFLAG=0
      IF (NPARITY.NE.0) IFLAG=94
      RETURN
C
C        ERROR MESSAGES
C
  960 IFLAG=91
      WRITE (6,961)
      RETURN
C
  950 IFLAG=90
      ENABLE=1
      RETURN
C
  970 IFLAG=91
      WRITE (6,971)
      RETURN
C
  980 IFLAG=92
      WRITE (6,981)
      RETURN
C
  990 WRITE (6,991) INUNIT
      IF (NPARITY.NE.0) GO TO 992
      BACKSPACE INUNIT
      NPARITY=1
      GO TO 5
  992 IFLAG=93
      RETURN
C
  961 FORMAT ("-  >>>ERROR<<<  VALUE OF IFLAG IS NEGATIVE")
  971 FORMAT ("-  >>>ERROR<<<  LOGICAL RECORD NUMBER REQUESTED EXCEEDS A
     C VAILABLE NUMBER OF RECORDS")
  981 FORMAT ("-  >>>ERROR<<<  DATA RECORD REQUESTED BEFORE HEADER RECORD
     & HAS BEEN READ")
  991 FORMAT ("-  >>>ERROR<<<  PARITY ERROR ENCOUNTERED DURING READ OPER
     C ATION ON UNIT ",I2)
      END
C
      SUBROUTINE SKIPBLK (INUNIT,N)
C
C        THIS ROUTINE SKIPS THE NEXT N RECORDS ON THE TAPE MOUNTED
C        ON LOGICAL UNIT INUNIT
C
C        ON RETURN, N INDICATES THE COMPLETION STATUS
C          N = 1   NORMAL RETURN
C          N = 2   EOF DETECTED
C          N = 3   PARITY ERROR DETECTED
C
      DIMENSION I(1)
      IF (N.EQ.0) N=1
      DO 20 J=1,N
      BUFFER IN (INUNIT,1) (I,I)
   10 IF (UNIT(INUNIT)) 20,200,300
   20 CONTINUE
      N=1
      RETURN
  200 N=2
      RETURN
  300 N=3
      RETURN
      END
C
      SUBROUTINE SKIPFILE(IUN,N)
      DO 10 I=1,N
10    CALL SKFILE(IUN)
      RETURN
      END
C
      FUNCTION IEVNT(E)
      DATA N4,N3,N2 /3584,498,56
      I=E
      I4=I/N4
      I3=(I-I4*N4)/N3
      I2=(I-I4*N4-I3*N3)/N2
      I1=I-I4*N4-I3*N3-I2*N2
      IF (I4.GT.0) I4=1000
      IF (I3.GT.0) I3=100
      IF (I2.GT.0) I2=10
      IF (I1.GT.0) I1=1
      IEVNT=I4+I3+I2+I1
      RETURN
      END
C
      FUNCTION ITME (T)
      I=T
      IH=I/3600
      IM=(I-IH*3600)/60
      IS=I-IH*3600-IM*60
      ITME=IS+IM*100+IH*10000
      RETURN
      END
